#pragma kernel GenerateDensity
#include "/Includes/Density.compute"
#include "/Includes/Noise.compute"

// Noise settings
StructuredBuffer<float3> offsets;
int octaves;
float lacunarity;
float persistence;
float noiseScale;
float noiseWeight;
float floorOffset;
float hardFloor;
float hardFloorWeight;

// Aditional, for programmer's free use
float4 params;

[numthreads(numThreads, numThreads, numThreads)]
void GenerateDensity(int3 id : SV_DispatchThreadID)
{
	if (id.x >= numPointsPerAxis || id.y >= numPointsPerAxis || id.z >= numPointsPerAxis) {
		return;
	}

	float3 sphereCenter = { 0.0f, 0.0f, 0.0f };
	float3 pos = centre + id * spacing - boundsSize / 2;
	float offsetNoise = 0;

	float noise = 0;
	float maxValue = 0;

	float frequency = noiseScale / 100;
	float amplitude = 1;
	float weight = 1;
	for (int j = 0; j < octaves; j++) 
	{
		float n = snoise((pos)*frequency + offsets[j]);
		n = 1 - abs(n);
		noise += n * amplitude;
		maxValue += amplitude;
		amplitude *= persistence;
		frequency *= lacunarity;
	}

	float finalVal = -1.0f;
	float noiseHeightOffset = noise * noiseWeight;

	if (length(pos - sphereCenter) < params.x + noiseHeightOffset) {
		finalVal = length(pos - sphereCenter) / (params.x + noiseHeightOffset);
		finalVal = (finalVal * 2) - 1;
		finalVal *= -1;
	}

	int index = indexFromCoord(id.x, id.y, id.z);
	points[index] = float4(pos, finalVal);
}

